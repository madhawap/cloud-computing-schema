BASE <https://w3id.org/cocoon/data/v1.0.1/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gr: <http://purl.org/goodrelations/v1#>
PREFIX schema: <https://schema.org/>
PREFIX cocoon: <http://w3id.org/cocoon/v1.0.1#>

GENERATE { 
    ?iri a cocoon:CloudStorageTransactionsPriceSpecification;
        rdfs:label ?transactions;
        schema:dateModified ?date;
        cocoon:hasProvider ?provider;
        GENERATE {
            ?iri  gr:hasPriceSpecification [ 
                a gr:CloudServicePriceSpecification ; 
                    gr:hasCurrency "USD"^^xsd:string; 
                    cocoon:hasCurrencyValue ?value;
                    cocoon:inRegion <Region/{?provider_slug}/{?region}>;
            ]
        }
        ITERATOR iter:JSONListKeys(?prices) AS ?region
        WHERE {
            BIND (xsd:decimal( fun:JSONPath(?prices,"$.['{?region}'].value") / 10000) AS ?value )
        } . 
}
SOURCE <https://raw.githubusercontent.com/miranda-zhang/cloud-computing-schema/master/example/jq/azure/2019-03-07/managed-disk-transactions.json> AS ?source
ITERATOR iter:JSONListKeys(?source) AS ?transactions
WHERE {
    BIND (fun:JSONPath(?source,"$.['{?transactions}']") AS ?transactions_json)
    BIND (fun:JSONPath(?transactions_json,"$.prices") AS ?prices)
    # having signle point of change for the following
    BIND ( "Azure" as ?provider_slug )
    BIND ( cocoon:Azure as ?provider )
    BIND ( "2019-03-07" as ?updated) # yr-month-day
    BIND ( <{?updated}/CloudStorageTransactionsPriceSpecification/{?provider_slug}/managed_disk/{?transactions}> as ?iri )
    BIND ( xsd:date(?updated) as ?date )
}
