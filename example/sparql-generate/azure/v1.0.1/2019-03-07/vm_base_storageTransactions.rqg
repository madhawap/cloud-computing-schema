BASE <https://w3id.org/cocoon/data/v1.0.1/> 
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gr: <http://purl.org/goodrelations/v1#>
PREFIX cocoon: <https://w3id.org/cocoon/v1.0.1/#>
PREFIX schema: <https://schema.org/>

GENERATE { 
  ?iri a cocoon:CloudStorageTransactionsPriceSpecification;
    rdfs:label "Azure VM Storage transactions price specification";
    schema:dateModified ?date;
    cocoon:hasProvider ?provider;
    gr:hasPriceSpecification [ 
        a cocoon:CloudServicePriceSpecification ; 
            gr:hasCurrency "USD"^^xsd:string; 
            cocoon:hasCurrencyValue ?value; 
            cocoon:inRegion <Region/{?provider_slug}/{?region}>;
    ] .
 
}
SOURCE <https://raw.githubusercontent.com/miranda-zhang/cloud-computing-schema/master/example/jq/azure/2019-03-07/vm_base_storageTransactions.json> AS ?source
ITERATOR iter:JSONListKeys(?source) AS ?region
WHERE {
    BIND (xsd:decimal ( fun:JSONPath(?source,"$.{?region}.value") / 10000 ) AS ?value)
    # having signle point of change for the following
    BIND ( "Azure" as ?provider_slug )
    BIND ( cocoon:Azure as ?provider )
    BIND ( "2019-03-07" as ?updated) # yr-month-day
    BIND ( <{?updated}/CloudStorageTransactionsPriceSpecification/{?provider_slug}/vm_base> as ?iri )
    BIND ( xsd:date(?updated) as ?date )
}
