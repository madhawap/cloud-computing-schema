BASE <https://w3id.org/cocoon/data/v1.0.1/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gr: <http://purl.org/goodrelations/v1#>
PREFIX cocoon: <http://w3id.org/cocoon/v1.0.1#>
PREFIX schema: <https://schema.org/>

GENERATE { 
    ?iri a cocoon:SystemImage;
        rdfs:label ?key;
        cocoon:hasProvider ?provider;
        schema:dateModified ?date;
        gr:hasPriceSpecification [ 
            a cocoon:CloudOSPriceSpecification ; 
                gr:hasCurrency "USD"^^xsd:string; 
                cocoon:hasCurrencyValue ?low;
                cocoon:forCoresLessEqual ?cores;
                cocoon:chargedPerCore ?percore
        ];
        gr:hasPriceSpecification [ 
            a cocoon:CloudOSPriceSpecification ; 
                gr:hasCurrency "USD"^^xsd:string; 
                cocoon:hasCurrencyValue ?high;
                cocoon:forCoresMoreThan ?cores;
                cocoon:chargedPerCore ?percore;
                cocoon:forCoresLessEqual ?cores2Times;
        ] .

    GENERATE {
        ?iri gr:hasPriceSpecification [ 
            a cocoon:CloudOSPriceSpecification ; 
                gr:hasCurrency "USD"^^xsd:string; 
                cocoon:hasCurrencyValue ?highest;
                cocoon:forCoresMoreThan ?cores2Times;
                cocoon:chargedPerCore ?percore;
        ] .
    } WHERE {
        FILTER( BOUND(?highest) )
    }    .

}
SOURCE <https://raw.githubusercontent.com/miranda-zhang/cloud-computing-schema/master/example/jq/gcloud/v1.62/os.json> AS ?source
ITERATOR  iter:JSONListKeys(?source) AS ?key
WHERE {
    BIND(fun:JSONPath(?source, '$.{?key}' ) AS ?OSPriceSpec)
    BIND( xsd:decimal( fun:JSONPath(?OSPriceSpec, '.low' ) ) AS ?low)
    BIND( xsd:decimal( fun:JSONPath(?OSPriceSpec, '.high' ) ) AS ?high)
    BIND( xsd:boolean( fun:JSONPath(?OSPriceSpec, '.percore' ) ) AS ?percore)
    BIND( xsd:decimal( fun:JSONPath(?OSPriceSpec, '.cores' ) ) AS ?cores)
    BIND( xsd:decimal( fun:JSONPath(?OSPriceSpec, '.highest' ) ) AS ?highest)
    BIND( xsd:decimal( IF( BOUND(?highest) , ?cores * 2 , ?undef ) ) AS ?cores2Times)
    # having signle point of change for the following
    BIND ( "Gcloud" as ?provider_slug )
    BIND ( cocoon:Gcloud as ?provider )
    BIND ( "2019-02-12" as ?release_date) # yr-month-day
    BIND ( <{?release_date}/SystemImage/{?provider_slug}/{?key}> as ?iri )
    BIND ( xsd:date(?release_date) as ?date )
}
